---
title: 'Scripted Plot Updates in Keynote'
date: 2020-10-07
permalink: /posts/2020/10/blog-post-1/
tags:
  - Keynote
  - Placeholders
  - AppleScript
  - Automator
---

Working in particle physics means I don't often get the feeling of producing something genuinely usefull for a broad (non-physicist) audience.
Today I have the pleasure of sharing something that upwards of dozens (there are dozens of us Michael, DOZENS!) of people might find game changing for their workflow.

Here's the informercial: Has this ever happened to you?! You spent hours producing hundreds of plots and figures to share with your colleagues only to realize your preliminary plots all boldly proclaim that they are "Preluminary".
Now you have to go through every slide painstakingly dragging the typo corrected PDFs of your figures into the corresponding Keynote Image Placeholders.
If only you could push a single button to tell Keynote to grab the updated file! Well today you can and I'm going to show you how!

There are two simple steps, first when you are making your Keynote presentation, each time you create an Image Placeholder, add a description containing the file path to the directory where the image is located:
<img src='/images/2020-10-07-blog-post-1/thesisDefense.png' width="100%" align="center">

Next open [Script Editor](https://support.apple.com/guide/script-editor/welcome/mac) and paste the following code into a new AppleScript:
```
--- Based on a script to perform this task where the files are grabed from URLs in the placeholder descriptions
--- https://iworkautomation.com/keynote/image-from-URL.html
tell application "Keynote"
	activate
	if not (exists front document) then error number -128
	tell the front document
		-- Loop over the slides in this presentation
		repeat with i from 1 to the count of slides
			tell slide i
				-- loop over the images in the slide
				repeat with q from 1 to the count of images
					
					-- Get the current image file name and description
					tell image q
						set imageFileName to get file name
						set currentDescription to the description
					end tell
					
					-- if the description is a file path, try updating the image
					if currentDescription contains ":" then
						-- try getting a new image with the same name at the path from the description
						set newImageFile to my fileFromPath(currentDescription, imageFileName)
						if newImageFile is not false then
							-- update the image and keep the current description
							set file name of image q to newImageFile
							set description of image q to currentDescription
						end if -- found a new image
					end if -- description looks like a file path
					
				end repeat -- images
			end tell -- this slide
		end repeat -- slides
	end tell -- this document
end tell -- keynote


on fileFromPath(filePath, fileName)
	try
		-- return an alias file reference to the image file
		set fullPath to (filePath & fileName)
		return alias fullPath
	on error
		-- try adding a colon between the filePath and fileName
		try
			set fullPath to (filePath & ":" & fileName)
			return alias fullPath
		on error -- adding a colon didn't fix it
			return false
		end try
	end try
end fileFromPath
```

When you run this script it will update all the images in Placeholders with a description containing a file path of the most recent Keynote presentation you had active. 